plugins {
    id "moop.android.test"
    alias(libs.plugins.baselineprofile)
}

android {
    namespace "soup.movie.macrobenchmark"
    defaultConfig {
        minSdk 28
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    targetProjectPath = ":app"

    // This code creates the gradle managed device used to generate baseline profiles.
    // To use GMD please invoke generation through the command line:
    // ./gradlew :app:generateBaselineProfile
    testOptions.managedDevices.devices {
        pixel6Api34(com.android.build.api.dsl.ManagedVirtualDevice) {
            device = "Pixel 6"
            apiLevel = 34
            systemImageSource = "google"
        }
    }
}

// This is the configuration block for the Baseline Profile plugin.
// You can specify to run the generators on a managed devices or connected devices.
baselineProfile {
    managedDevices += "pixel6Api34"
    useConnectedDevices = true
}

dependencies {
    implementation libs.androidx.test.ext.junit
    implementation libs.androidx.test.espresso
    implementation libs.androidx.test.uiautomator
    implementation libs.androidx.benchmark.macro.junit4
}

androidComponents {
    onVariants(selector().all()) { v ->
        def artifactsLoader = v.artifacts.getBuiltArtifactsLoader()
        v.instrumentationRunnerArguments.put(
                "targetAppId",
                v.testedApks.map { artifactsLoader.load(it)?.applicationId }
        )
    }
}
