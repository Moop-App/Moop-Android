import dependencies.Libs

buildscript {
    repositories {
        google()
        mavenCentral()

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.plugin
        classpath Libs.Kotlin.serializationPlugin
        classpath Libs.Dagger.hiltPlugin
        classpath Libs.AndroidX.Navigation.safeArgsPlugin
        classpath Libs.Google.gmsPlugin
        classpath Libs.Firebase.crashlyticsPlugin
        classpath Libs.Firebase.perfPlugin
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.12.4'
}

Plugin.metaClass.isAndroidApp = {-> delegate.class.getCanonicalName() == "com.android.build.gradle.AppPlugin" }
Plugin.metaClass.isDynamicFeature = {-> delegate.class.getCanonicalName() == "com.android.build.gradle.DynamicFeaturePlugin" }
Plugin.metaClass.isAndroidLibrary = {-> delegate.class.getCanonicalName() == "com.android.build.gradle.LibraryPlugin" }

allprojects {
    repositories {
        google()
        mavenCentral()

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.google.com/' }
        maven { url 'https://devrepo.kakao.com/nexus/content/groups/public/' }
        maven { url 'https://naver.jfrog.io/artifactory/maven/' }
    }
    plugins.whenPluginAdded {
        if (it.isAndroidApp() || it.isAndroidLibrary() || it.isDynamicFeature()) {
            android.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all {
        kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint("0.41.0")
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }
    afterEvaluate {
        tasks.find {
            it.name.contains("preBuild")
        }.each {
            it.dependsOn spotlessApply
        }
    }
}

Object propOrDef(String propertyName, Object defaultValue) {
    def properties = new Properties()
    def keyFile = rootProject.file("signing/key.properties")
    if (keyFile.exists()) {
        keyFile.withInputStream { stream -> properties.load(stream) }
    }
    def propertyValue = properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}
