plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "dagger.hilt.android.plugin"
    id "androidx.navigation.safeargs.kotlin"
    id "moop.android.library.compose"
    id "moop.spotless"
}

apply from: rootProject.file('gradle/android.gradle')

android {
    namespace "soup.movie.feature.common"
    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules-navigation.pro'
        }
    }
}

dependencies {
    api project(':common')
    api project(':model')
    api project(':data-repository')

    api libs.kotlin.stdlib
    api libs.coroutines.core
    api libs.coroutines.android

    api libs.androidx.appcompat
    api libs.androidx.core

    implementation libs.compose.material
    api libs.compose.materialIconsExtended
    implementation libs.compose.ui

    implementation libs.androidx.browser
    implementation libs.androidx.startup
    implementation libs.androidx.window

    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.compiler

    implementation libs.androidx.navigation.fragment
    implementation libs.androidx.navigation.dynamicFeaturesFragment

    implementation libs.dagger.hilt.android
    kapt libs.dagger.hilt.compiler

    implementation libs.coil.runtime

    implementation platform(libs.firebase.bom)
    implementation firebase.dynamicLinks
    implementation libs.google.ads

    api libs.threetenAbp
    api libs.timber
    implementation libs.kakaoLink

    testImplementation project(':testing')
    androidTestImplementation project(':testing')
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = [
                '-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
                '-opt-in=kotlinx.coroutines.ObsoleteCoroutinesApi'
        ]
    }
}

task buildNavigationProguard {
    def navigationGraphXml = project.file("src/main/res/navigation/nav_graph.xml")
    def proguardRules= project.file("proguard-rules-navigation.pro")
    doLast {

        // XmlParser didn't work so let me use the dirty hack
        if (!navigationGraphXml.text.contains("xmlns:app=\"http://schemas.android.com/apk/res-auto\"")) {
            throw new GradleScriptException("the namespace has been changed from app")
        }

        def fqdns = []

        navigationGraphXml.eachLine {
            def line = it.trim()

            if (line.startsWith("android:name=\"")) {
                def className = line.substring("android:name=\"".length(), line.lastIndexOf("\""))

                if (className.contains(".")) {
                    fqdns << className
                }
            } else if (line.startsWith("app:argType=\"")) {
                def className = line.substring("app:argType=\"".length(), line.lastIndexOf("\""))

                if (className.contains(".")) {
                    fqdns << className
                }
            }
        }

        proguardRules.write(fqdns.sort().collect {
            "-keepnames class $it"
        }.join("\n"))
    }
}

preBuild.dependsOn(buildNavigationProguard)
